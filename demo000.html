<html>

<head>
<title>Demo ver0.1</title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<script type="text/javascript" src="gl-matrix-min.js"></script>

<script type="text/javascript" src="webgl-utils.js"></script>
<script type="text/javascript" src="cannon.min.js"></script>

<script id="shader-fs" type="x-shader/x-fragment">#version 300 es

precision mediump float;

in vec2 vTextureCoord;
in vec3 vTransformedNormal;
in vec4 vPosition_cameraSpace;

uniform float uMaterialShininess;

uniform bool uShowSpecularHighlights;
uniform bool uUseLighting;

uniform vec3 uAmbientColor;

uniform vec3 uPointLightingLocation;
uniform vec3 uPointLightingSpecularColor;
uniform vec3 uPointLightingDiffuseColor;

uniform sampler2D uSampler;

out vec4 outputColor;

void main(void) {
    vec3 lightWeighting;
    if (!uUseLighting) {
        lightWeighting = vec3(1.0, 1.0, 1.0);
    } else {
        vec3 lightDirection = normalize(uPointLightingLocation - vPosition_cameraSpace.xyz);
        vec3 normal = normalize(vTransformedNormal);

        float specularLightWeighting = 0.0;
        if (uShowSpecularHighlights) {
            vec3 eyeDirection = normalize(-vPosition_cameraSpace.xyz);
            vec3 reflectionDirection = reflect(-lightDirection, normal);

            specularLightWeighting = pow(max(dot(reflectionDirection, eyeDirection), 0.0), uMaterialShininess);
        }

        float diffuseLightWeighting = max(dot(normal, lightDirection), 0.0);
        lightWeighting = uAmbientColor
            + uPointLightingSpecularColor * specularLightWeighting
            + uPointLightingDiffuseColor * diffuseLightWeighting;
    }

    vec4 fragmentColor = texture(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));
    outputColor = vec4(fragmentColor.rgb * lightWeighting, fragmentColor.a);
}

</script>

<script id="shader-vs" type="x-shader/x-vertex">#version 300 es

in vec3 aVertexPosition;
in vec3 aVertexNormal;
in vec2 aTextureCoord;

uniform mat4 uMVMatrix;
uniform mat4 uPMatrix;
uniform mat3 uNMatrix;

out vec2 vTextureCoord;
out vec3 vTransformedNormal;
out vec4 vPosition_cameraSpace;


void main(void) {
    vPosition_cameraSpace = uMVMatrix * vec4(aVertexPosition, 1.0);
    gl_Position = uPMatrix * vPosition_cameraSpace;
    vTextureCoord = aTextureCoord;
    vTransformedNormal = uNMatrix * aVertexNormal;
}

</script>

<script type="text/javascript" src="myUtils.js"></script>
<script type="text/javascript" src="control.js"></script>
<script type="text/javascript" src="main.js"></script>

</head>


<body onload="webGLStart();">
    <canvas id="display" style="border: none;" width="800" height="800"></canvas>
    <div id="loadingtext">Loading world...</div>
</body>

</html>
